import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import obo.facts.*;
import java.text.*;
import treasury.utils.*;
import treasury.facts.*;

public class BuildingPermitOnlineService  {
	
	@DataContext("building_permit")
	def appEm;

	@DataContext("building_permit_entity")
	def entityEm;

	@DataContext("building_permit_accessory")
	def accessoryEm;

	@DataContext("building_permit_ancillary")
	def ancillaryEm;

	@DataContext("building_permit_rpu")
	def rpuEm;

	@DataContext("building_permit_info")
	def appInfoEm;

	@DataContext("building_permit_professional")
	def professionalEm;

	@DataContext("building_permit_requirement")
	def reqEm;

	@DataContext("obo_requirement_type")
	def reqTypeEm;

	@Service("OboConstructionCostService")
	def costSvc;

	@Service("DateService")
	def dateSvc;

	@Service("WorkflowTaskService")
	def wfSvc;

	@Service(value="OnlineBuildingPermitDownloadService", connection="cloud-obo-server")
    def cloudBldgSvc;

    @Env
    def env;

    @ProxyMethod
    public def getApplication( def o  ) {
		return cloudBldgSvc.getDownloadInfo( [appid: o.appid, orgcode: env.ORGID ] );
    }

	@ProxyMethod
	public def upload( def app ) {
		def applicantid = app.remove("applicantid");
		def accessoryid = app.remove("accessoryid");
		def newapp = appEm.create( app );
		app.entities.each {
			it.appid = newapp.objid;
			entityEm.create( it );
		}
		app.rpus.each {
			it.appid = newapp.objid;
			it.owner = [objid: it.ownerid ];
			rpuEm.create( it );
		}
		app.professionals.each {
			it.appid = newapp.objid;
			it.entity = [objid: it.entityid ];
			professionalEm.create( it );
		}
		app.accessories.each {
			it.appid = newapp.objid;
			it.occupancytype = [objid: it.occupancytypeid];
			accessoryEm.create( it );
		}
		app.ancillaries.each {
			it.appid = newapp.objid;
			it.type = [ permittypeid: it.permittypeid ];
			ancillaryEm.create( it );
		}
		app.infos.each {
			it.appid = newapp.objid;
			it.type = [name: it.name ];
			appInfoEm.create( it );
		}
		def u = [:];
		u.applicantid = applicantid;
		if( accessoryid ) u.accessoryid = accessoryid;
		appEm.find( [objid: newapp.objid ] ).update( u );

		//immediatly accept
		accept( newapp );
		return newapp;
	}

	@ProxyMethod
	public def accept( def app ) {
		def dt = dateSvc.getServerDate();

		//calculate the cost first before receiving
		def param = [:];
		param.app = [apptype:app.apptype, totalfloorarea:app.totalfloorarea];
		param.occupancytype = [
			group: app.occupancytype.group.objid,
			division: app.occupancytype.division.objid,
			type: app.occupancytype.objid
		] 
		def cost = costSvc.calc( param );
		def u = [:];
		u.fixedcost = cost;

		appEm.find( [objid: app.objid ] ).update( u );

		//update the requirements
		def reqTypes = reqTypeEm.where("1=1").list();
		reqTypes.each {
			def r = [:];
			r.appid = app.objid;
			r.state = 0;
			r.typeid = it.objid;
			r.type = [objid: it.objid];
			r.checked = 0;
			reqEm.create( r );
		}

		//fire the workflow to start the process
		def p = [:];
		p.processname = 'building_permit';
		p.refid = app.objid;
		p.startdate = dt;
		p.info = app;
		wfSvc.start(p);	
		return u;
	}
	

}