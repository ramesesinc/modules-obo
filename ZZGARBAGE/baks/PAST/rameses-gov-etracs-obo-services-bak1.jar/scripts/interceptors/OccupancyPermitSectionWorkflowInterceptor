import com.rameses.annotations.*; 

class OccupancyPermitSectionWorkflowInterceptor { 

	@Service("WorkflowTaskService")
	def wfSvc;

	@Service("DateService")
	def dateSvc;

	@DataContext("occupancy_permit_finding")
	def findingEm;

	@DataContext("vw_occupancy_permit_section")
	def sectionEm;

	@DataContext("occupancy_permit_fee")
	def feeEm;

	@DataContext("occupancy_permit")
	def appEm;

	@DataContext("obo_section")
	def oboSectionEm;

	@Before(pattern="WorkflowTaskService.signal", eval="#{args[0].processname == 'occupancy_permit_section'  && args[0].action == 'send-for-revision' }") 
	public void beforeSendForRevision( def evt ) {
		def p = evt.args[0];
		def list = findingEm.find( [parentid: p.refid ] ).where("supersederid IS NULL AND state = 2 AND transmittalid IS NULL").list();
		if(!list) throw new Exception("There must be at least one finding");
	} 

	@Before(pattern="WorkflowTaskService.signal", eval="#{args[0].processname == 'occupancy_permit_section'  && args[0].action == 'approve' }") 
	public void beforeApproval( def evt ) {
		def p = evt.args[0];
		def list = findingEm.find( [parentid: p.refid ] ).where("supersederid IS NULL AND state = 2 AND NOT(transmittalid IS NULL)").list();
		if(list) throw new Exception("There must be no finding. If there are findings select Send for Revision instead");

	} 

	//signal the parent 
	@After(pattern="WorkflowTaskService.signal", eval="#{args[0].processname == 'occupancy_permit_section' && args[0].action.matches('send-for-revision|approve') }") 
	public void signalParent( def evt ) {
		def p = evt.args[0];
		def r = evt.result;
		def appid = sectionEm.find( [objid: p.refid] ).select("appid").val();
		def cnt = sectionEm.find([appid:appid]).select("c:{COUNT(*)}").where( " task.state NOT IN ('obo-processing', 'end', 'for-revision' ) ").val();
		if(cnt <=0) {
			def app = appEm.find([objid:appid]).select("task.*").first();
			def f = [:];
			f.processname = 'occupancy_permit';
			f.refid = appid;
			f.startdate = dateSvc.getServerDate();
			f.info = [ app: p.info ];
			f.action = (r.state == 'end') ? 'send-for-release' : 'send-for-verification';
			f.taskid = app.taskid;
			wfSvc.signal(f);	
		}
	} 




} 