import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import obo.facts.*;
import java.text.*;

public class OnlineBuildingApplicationService  {

	@DataContext("online_building_application")
	def appEm;

	@DataContext("online_building_application_entity")
	def entityEm;

	@DataContext("online_building_application_ancillary")
	def ancillaryEm;

	@DataContext("online_building_application_location")
	def locationEm;

	@DataContext("online_building_application_info")
	def appInfoEm;

	@Service("DateService")
	def dateSvc;

	@Service(dynamic=true)
	def dynamicSvc;



	/* ----------------------------------------------------------------
	/ This method takes care of updating the next step. It starts by 
	/ knowing first current step. then based on conditions it will determine
	/ the next step. For the steps, see getAppDSte
	/------------------------------------------------------------------*/
	@ProxyMethod
	public def getAppSteps() {
		def list = [];
		list << [step:0, name:'initial', caption: "Initial"];
		list << [step:1, name:'location', caption:'Location'];	
		list << [step:2, name:'owner', caption:'Applicant'];
		list << [step:3, name:'professional', caption:'Professionals'];
		list << [step:4, name:'project', , caption:'Project Details'];
		list << [step:5, name:'accessories',caption:'Accessories'];
		list << [step:6, name:'ancillarylist', caption:'Ancillary Permits'];
		list << [step:7, name:'requirementlist', caption:'Requirements'];	
		return list;
	}

	@ProxyMethod
	public def moveNextStep( def o ) {
		if(!o.appid) throw new Exception("appid is required in determining the next step");
		def app = appEm.find( [objid: o.appid] ).first();
		def step = app.step;

		//check if step is professional but does not have any entry
		if( step == 3 ) {
			def cnt = entityEm.find( [professional:1, appid: o.appid ] ).select("c:{COUNT(*)}").val();
			if( cnt == 0 ) throw new Exception("Please add at least one professional");
		}

		def nextStep = step + 1;
		if( nextStep == 2 ) {
			if(app.ownerid) nextStep = 3; 
		}
		//compare next step if less than existing then you can update otherwise stay the same
		if(app.step < nextStep ) {
			appEm.find( [objid: o.appid] ).update([step: nextStep]);			
		}
		else {
			nextStep = app.step;
		}
		return [step:nextStep];
	}

	@ProxyMethod
	public def create(def o) {
		def kg = new KeyGen();
		o.objid = kg.generateAlphanumKey(o.orgcode+"-",8);
		o.dtfiled = dateSvc.getServerDate();
		o.step = 1;
		def app = appEm.create( o );
		/*
		def emailSvc = dynamicSvc.lookup(o.orgcode+":EmailService");
		app.tokenid = "MYTOKEN " + new UID();
		emailSvc.send( app );
		*/
		return app;
	}

	def formatAddress( def addr ) {
		def lst = [];
		lst << [addr.unitno, addr.bldgno, addr.bldgname ].findAll{it!=null}.join(" ");
		lst << [addr.street, addr.subdivision ].findAll{it!=null}.join(",");
		lst << [addr.barangay?.name, addr.citymunicipality, addr.province ].findAll{it!=null}.join(",");
		return lst.findAll{it}.join(",");		
	}

	def formatPersonalName( def v ) {
		return v.lastname + ", " + v.firstname + " " + ((v.middlename!=null)?v.middlename.substring(0,1)+'.': '');
	}

	//reusable method of saving entity
	def saveEntity(def o) {
		if(!o.objid) {
			def v = [appid: o.appid];
			v.putAll(o);
			if(o.owner == null) v.owner = 0;
			if(o.lotowner == null) v.lotowner = 0;
			if(o.professional == null) v.professional = 0;
			v.name = formatPersonalName(o);
			v.address.text = formatAddress(o.address);
			return entityEm.create(v);
		}
		else {
			o.name = formatPersonalName(o);
			o.address.text = formatAddress(o.address);
			return entityEm.find( [objid: o.objid] ).update( o );
		}
	}


	@ProxyMethod
	public def findCurrentInfo( def o ) {
		appEm.find( [objid: o.appid ] ).select("orgcode,step").first();
	}

	//STEP 1.
	@ProxyMethod
	public def saveLocation(def o) {
		def lotowner = o.remove("owner");
		lotowner.appid = o.appid;
		lotowner.entityid = lotowner.remove("objid");
		lotowner.lotowner = 1;
		if( o.lotowned == "1" ) lotowner.owner = 1;
		def entityOwner = saveEntity(lotowner);
		o.ownerid = entityOwner.objid;
		o.owner = [objid: o.ownerid ];
		def loc = locationEm.create( o );
		def  u = [locationid: loc.objid ];
		if( o.lotowned == "1" ) {
			u.ownerid = entityOwner.objid;
		}
		appEm.find( [objid: o.appid ] ).update(u);
		return u;
	}

	@ProxyMethod
	public def getLocation(def o ) {
		def app = appEm.select("locationid").find( [objid: o.appid ] ).first();
		if( !app?.locationid ) return null;
		return locationEm.find( [objid: app.locationid ] ).first();
	}

	@ProxyMethod
	public def getLocations(def o ) {
		def app = appEm.select("locationid").find( [objid: o.appid ] ).first();
		def locid = app.locationid;
		def list =  locationEm.find( [appid: o.appid ] ).list();
		list.each {
			it.primary =  (it.objid == locid ) ? true : false;
		}
		return list;
	}

	//STEP 2
	@ProxyMethod
	public def saveOwner(def o) {
		o.owner = 1;
		def owner = saveEntity(o);
		appEm.find([objid:o.appid]).update([ownerid: owner.objid]);
	}

	@ProxyMethod 
	public def getOwner(def o) {
		def app = appEm.select("ownerid").find( [objid: o.appid ] ).first();
		if(!app.ownerid) return null;
		return entityEm.find( [objid: app.ownerid ] ).first();		
	}

	@ProxyMethod 
	public def saveProfessional(def o) {
		o.professional = 1;
		saveEntity(o);
	}

	@ProxyMethod 
	public def getProfessional(def o) {
		return entityEm.find( [objid: o.objid]).first();
	}

	@ProxyMethod
	public void removeProfessional( def o) {
		entityEm.find( [objid: o.objid]).delete();
	}

	@ProxyMethod
	public def getProfessionalList( def o ) {
		def flds = "objid,name,profession,address.text,prc.*";
		return entityEm.select(flds).find([appid: o.appid]).where('professional = 1').list();
	}

	@ProxyMethod
	public def updateProjectInfo( def o  ) {
		appEm.find( [objid: o.appid] ).update( o );
	}

	@ProxyMethod
	public def getProjectInfo( def o  ) {
		def flds = "description,title,occupancytypeid,numunits,floorarea,estimatedcost,dtproposedconstruction,dtexpectedcompletion,supervisorid,totalfloorarea,bldgtypeid,height,numfloors,worktypes";		
		return appEm.select(flds).find( [objid: o.appid] ).first();
	}


	@ProxyMethod
	public def getApplication(def o ) {
		return appEm.find( [objid: o.objid] ).first();
	}

	@ProxyMethod
	public def saveAncillaryPermits( def o ) {
		o.permits.each { k,v->
			def z = ancillaryEm.find( [appid:o.appid, permittypeid:k ] ).select("objid").val();
			if( !z && v.toInteger() == 1 ) {
				def m = [appid: o.appid, permittypeid: k, state: 'PENDING' ];
				m.type = [objid: k];
				ancillaryEm.create( m );	
			} 
			else if( z && v.toInteger() == 0 ) {
				ancillaryEm.find( [appid: o.appid, permittypeid: k] ).delete()	
			}
		}		
		return null;
	}

	@ProxyMethod
	public def getAncillaryPermits( def o ) {
		return ancillaryEm.find( [appid: o.appid] ).orderBy("type.sortorder").list();	
	}

	@ProxyMethod
	public def getAncillaryPermit( def o ) {
		if(!o.objid) throw new Exception("getAncillary error. objid is required");
		
		def m = ancillaryEm.find( [objid: o.objid] ).first();
		m.infos = getAppInfos( [parentid: o.objid ] );
		//m.requirements = getRequirements( o );
		return m;	
	}

	@ProxyMethod
	public void saveAncillaryPermit( def o ) {
		if(!o.objid) throw new Exception("updateAncillary error. objid is required");
		//if(!o.infos) throw new Exception("updateAncillary error. infos is required");
		o.designprofessionalid = o.designprofessional?.objid;
		o.supervisorid = o.supervisor?.objid;
		ancillaryEm.find( [objid: o.objid ] ).update( o );
		saveAppInfos( [parentid: o.objid, infos: o.infos ] );
	}

	@ProxyMethod
	public def saveAccessories( def o ) {
		if(!o.objid) throw new Exception("updateAccessories error. objid is required");
		if(!o.infos) return;
		saveAppInfos( [parentid: o.objid, infos: o.infos ] );
	}

	@ProxyMethod
	public def getAccessories( def o ) {
		if(!o.objid) throw new Exception("updateAccessories error. objid is required");
		return getAppInfos( [parentid: o.objid ] );
	}


	//UTILITY METHODS FOR APP INFOS
	private void saveAppInfos( o ) {
		if(!o.parentid) throw new Exception("saveAppInfos error. parentid is required");
		//if(!o.infos) throw new Exception("saveAppInfos error. infos is required");

		//clear first existing infos.
		appInfoEm.find([parentid: o.parentid]).delete();
		o.infos.each { info->
			def m = [parentid: o.parentid, name: info.name];
			m.type = [objid: info.name];
			if( info.datatype == 'decimal') m.decimalvalue = info.value;
			else if( info.datatype == 'integer') m.intvalue = info.value;
			else if( info.datatype == 'boolean') m.booleanvalue = true;
			else if( info.datatype == 'date') m.datevalue = info.value;
			else m.stringvalue = info.value;
			appInfoEm.create( m );
		}
	}

	private def getAppInfos( o ) {
		def list = [];
		def tmpList = appInfoEm.find( [parentid: o.parentid] ).list();
		tmpList.each { info->
			def m = [name:info.name, datatype:info.type.datatype];
			if( m.datatype == 'decimal') m.value = info.decimalvalue;
			else if( m.datatype == 'integer') m.value = info.intvalue;
			else if( m.datatype == 'boolean') m.value = true;
			else if( m.datatype == 'date') m.value = info.datevalue;
			else m.value = info.stringvalue;
			list << m;
		}
		return list;
	}



}